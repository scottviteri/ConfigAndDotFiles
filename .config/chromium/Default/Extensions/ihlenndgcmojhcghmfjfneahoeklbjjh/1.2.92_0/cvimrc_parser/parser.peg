{
  var scopeIdentifiers = {};
  var autoJsLength = 0;
  function merge(a, b) {
    for (var p in b) {
      if (typeof b[p] === 'object' && !Array.isArray(b[p])) {
        if (!a.hasOwnProperty(p)) {
          a[p] = {};
        }
        merge(a[p], b[p]);
      } else {
        a[p] = b[p];
      }
    }
  }
  function parseScope(data) {
    var result = {MAPPINGS: []};
    data.forEach(function(e) {
      if (e === null)
        return;
      if (e.MAPPINGS) {
        result.MAPPINGS.push(e.MAPPINGS);
      } else {
        merge(result, e);
      }
    });
    result.MAPPINGS = result.MAPPINGS.join('\n');
    return result;
  }
  function objectPlural(key) {
    var replacements = {
      qmark: 's',
      searchengine: 's',
      searchalias: 'es'
    };
    if (replacements.hasOwnProperty(key))
      key += replacements[key];
    return key;
  }
}

Start = Scope

EOF = !.
EOL = [\n\r]
WhiteSpace = [ \t]
MapIdentifier = a:(!WhiteSpace !EOL .)+ { return text(); }
LineSeparator
  = WhiteSpace* EOL+ WhiteSpace* "\\" WhiteSpace* { return null; }
_ = (WhiteSpace / LineSeparator)+
__ = (WhiteSpace / LineSeparator)*

Identifier = ([@][@])?[a-zA-Z_$][a-zA-Z_$0-9]* { return text(); }

LET     = 'let'
SET     = 'set'
SITE    = 'site'
CALL    = 'call'
MAP     = 'map'
IMAP    = 'imap'
UNMAP   = 'unmap'
IUNMAP  = 'iunmap'
COMMAND = 'command'

Comment
  = WhiteSpace* '"' [^\n]* { return null; }

StringCharacter
  = '\\' a:[^\n] { return a; }
  / [^\n]
  / _ { return ''; }

DoubleStringCharacter
  = !'"' a:StringCharacter { return a; }
SingleStringCharacter
  = !'\'' a:StringCharacter { return a; }

StringLiteral
  = '"' a:DoubleStringCharacter* '"' { return a.join(''); }
  / '\'' a:SingleStringCharacter* '\'' { return a.join(''); }

FloatingPointLiteral
  = IntegerLiteral '.' ('0'* IntegerLiteral / '0'+)? {
    return parseFloat(text());
  }
  / '.' ('0'* IntegerLiteral / '0'+) { return parseFloat(text()); }

IntegerLiteral
  = '0' { return 0; }
  / [1-9][0-9]* { return parseInt(text(), 10); }

NumberLiteral
  = FloatingPointLiteral
  / IntegerLiteral

ExpressionList
  = head:(
    __ expr:Expression {
      return expr;
    }
  )
  tail:(
    __ ',' __ expr:Expression {
      return expr;
    }
  )*
  {
    return [head].concat(tail);
  }

ArrayLiteral
  = '[' __ ']' { return []; }
  / '[' elems:ExpressionList __ ']' {
    return elems;
  }

MemberExpression
  = head:(
    Identifier
  )
  tail:(
    __ '[' __ a:Expression __ ']' {
      return a;
    }
  )*
  {
    var result = scopeIdentifiers[head];
    tail.forEach(e => result = result[e]);
    return result;
  }

Expression
  = StringLiteral
  / NumberLiteral
  / ArrayLiteral
  / MemberExpression

LetStatement
  = LET _ a:Identifier __ '=' __ b:Expression {
    scopeIdentifiers[a] = b;
    var r = {}; r[a] = b; return r;
  }
  / LET _ a:Identifier _ b:(Identifier / NumberLiteral) __ '=' __ c:Expression {
    scopeIdentifiers[a] = {[b]: c};
    a = objectPlural(a);
    var r = {}; r[a] = {}; r[a][b] = c; return r;
  }

SetStatement
  = SET _ 'no' a:Identifier { var r = {}; r[a] = false; return r; }
  / SET _ !'no' a:Identifier { var r = {}; r[a] = true; return r; }

SiteStatement
  = SITE _ a:StringLiteral __ '{' WhiteSpace* EOL b:Scope '}' {
    var r = {sites: {}}; r.sites[a] = b; return r;
  }

UnmapIdentifierList
  = __ a:MapIdentifier _ b:UnmapIdentifierList __ { return [a].concat(b); }
  / __ a:MapIdentifier __ { return [a]; }

UnmapStatement
  = a:(UNMAP / IUNMAP) _ b:UnmapIdentifierList {
    return { MAPPINGS: a + ' ' + b.join(' ') };
  }

MapStatement
  = a:(MAP / IMAP) _ b:MapIdentifier _ c:[^\n]* {
    return { MAPPINGS: [a, b, c.join('')].join(' ') };
  }

CallStatement
  = CALL _ a:[^\n]* {
    return { MAPPINGS: 'call ' + a.join('') };
  }

CommandStatement
  = COMMAND _ a:[a-zA-Z]+ _ b:[^\n]+ {
    var r = { COMMANDS: {} };
    r.COMMANDS[a.join('')] = b.join('');
    return r;
  }

JavaScriptBlockChar
 = (!'}}' .) { return text(); }

Parameters
  = a:Identifier __ ',' __ b:Parameters {
    return [a].concat(b);
  }
  / a:Identifier { return [a]; }

ParameterList
  = '(' __ a:Parameters? __ ')' { return a || []; }

JavaScriptBlock
  = '{{' EOL* a:JavaScriptBlockChar* '}}' {
    return a.join('');
  }

AutoRunJavaScriptBlock
  = '->' __ a:JavaScriptBlock {
    var result = {
      AUTOFUNCTIONS: {}
    };
    result.AUTOFUNCTIONS[autoJsLength] = a;
    autoJsLength++;
    return result;
  }

JavaScriptBlockDefinition
  = a:Identifier __ b:ParameterList? __ '->' __ c:JavaScriptBlock {
    var result = {
      FUNCTIONS: {
      }
    };
    result.FUNCTIONS[a] = '(function(' + (b || []).join(',') + ')' +
                          '{' + c + '})';
    return result;
  }

StatementValue
  = LetStatement
  / JavaScriptBlockDefinition
  / AutoRunJavaScriptBlock
  / SetStatement
  / SiteStatement
  / ('unmapAll' / 'iunmapAll') {
    return {MAPPINGS: text() };
  }
  / CallStatement
  / CommandStatement
  / MapStatement
  / UnmapStatement
  / Comment

Statement
  = (WhiteSpace / EOL)* a:StatementValue Comment? (EOL / WhiteSpace)* { return a; }

Scope
  = (WhiteSpace / EOL)* EOF { return parseScope([]); }
  / a:Statement* { return parseScope(a); }
